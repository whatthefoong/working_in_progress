*temp choice 1

*label screen
[b]Character Details[/b]
*stat_chart
	text Rank
	text Name
	text Age
	text Race
	text Background
	text Dollars
*if choice = 1
	*goto charstats
*if choice = 2
	*goto men_logs
*if choice = 3
	*goto char_rs
*if choice = 4
	*goto stat_glossary

*label menu1
*choice
	# Men and Logistics
		*set choice 2
		*goto screen
	#Relationships
		*set choice 3
		*goto screen
	#Stat Glossary
		*set choice 4
		*goto stat_glossary

*label menu2
*choice
	# Character Stats
		*set choice 1
		*goto screen
	#Relationships
		*set choice 3
		*goto screen
	#Stat Glossary
		*set choice 4
		*goto stat_glossary

*label menu3
*choice
	# Character Stats
		*set choice 1
		*goto screen
	# Men and Logistics
		*set choice 2
		*goto screen
	#Stat Glossary
		*set choice 4
		*goto stat_glossary

*label menu4
*choice
	#Character Stats
		*set choice 1
		*goto screen
	# Men and Logistics
		*set choice 2
		*goto screen
	#Relationships
		*set choice 3
		*goto screen

*label charstats
*line_break
[b]Main Traits[/b]
*stat_chart
	percent Physical
	percent Finesse
	percent Charm
	percent Intellect
*line_break
[b]Personality[/b]
*stat_chart
	opposed_pair mind
		Extrovert
		Introvert
	opposed_pair energy
		Intuitive
		Observant
	opposed_pair nature
		Logical
		Emotional
	opposed_pair tactics
		Improvisational
		Structured
*line_break
[b]Specific Skills[/b]
*stat_chart
	text Demolitions
	text Driving
	text Engineering
	text Logistics
	text Marksmanship
	text Scouting
	text Strategy
	text Swimming
*line_break
*goto menu1

*label men_logs
*line_break
[b]Command[/b]
*stat_chart
	text Men
*line_break
[b]Ammunition[/b]
*stat_chart
	text light_rounds Light Rounds
	text heavy_rounds Heavy Rounds
*line_break
[b]Explosives[/b]
*stat_chart
	text demolition_charge Demolition Charge(s)
	text flash_bang Flash Bang(s)
	text he_grenade HE Grenade(s)
	text smoke_grenade Smoke Grenade(s)
	text rpg_ammo RPG Ammo
*line_break
*goto menu2

*label char_rs
*line_break
[b]Relationships[/b]
*stat_chart
	percent Renown
*line_break
Factions:
*stat_chart
	percent MPRA
	percent LOM
	percent DFFM
	percent USS
*line_break
*goto menu3

*label stat_glossary
[b]Main Traits[/b]
*line_break
Physical: Influences your strength, endurance and physique.
*line_break
Finesse: Influences your accuracy, agility and stealth.
*line_break
Charm: Influences your tact and leadership.
*line_break
Intellect: Influences your learning and perceptiveness.


[b]Reputation[/b]
*line_break
Renown: How well known you are.

[b]Personality[/b]
*line_break
Mind:
*line_break
Extrovert - Prefer social situations and enjoys interaction.
*line_break
Introvert - Prefer solitary activities and gets exhausted by interaction.


Energy:
*line_break
Intuitive - Imaginative, creative and curious.
*line_break
Observant - Practical, logical and systematic.


Nature:
*line_break
Emotional - Emotionally sensitive, focuses on harmony.
*line_break
Logical - Emotionally objective, focuses on efficiency.


Tactics:
*line_break
Improvisational - Flexible and improvisational.
*line_break
Structured - Organized and rigid.
*line_break
*goto menu4


